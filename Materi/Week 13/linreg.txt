MACRO

LINREG X Y REG ALPHA X1

MCOLUMN X Y REG
MCONSTANT N I RATAX RATAY B1 B0 SUM SSR SSE SST DFR DFE DFT MSR MSE F ALPHA ALPHA1 FTAB P PVAL ALPHA2 SE1 T1 DF TTAB1 P1 PVAL1 SE0 T0 TTAB0 P0 PVAL0 X1 NILAI 
LET N=COUNT(X)
LET RATAX=MEAN(X)
LET RATAY=MEAN(Y)
LET B1=0
LET SUM=0
DO I=1:N
LET B1=B1+(X(I)-RATAX)*(Y(I)-RATAY)
LET SUM=SUM+(X(I)-RATAX)^2
ENDDO
LET B1=B1/SUM
LET B0=RATAY-(B1*RATAX)
DO I=1:N
LET REG(I)=B0+B1*X(I)
ENDDO
PRINT B0 B1

LET SSR=0
LET SST=0
DO I=1:N
LET SSR=SSR+(REG(I)-RATAY)^2
LET SST=SST+(Y(I)-RATAY)^2
ENDDO
LET SSE=SST-SSR
LET DFR=1
LET DFT=N-1
LET DFE=DFT-DFR
LET MSR=SSR/DFR
LET MSE=SSE/DFE
LET F=MSR/MSE

LET ALPHA1=1-ALPHA
INVCDF ALPHA1 FTAB;
F DFR DFE.

CDF F P;
F DFR DFE.
LET PVAL=1-P
PRINT SSR SSE SST F FTAB ALPHA PVAL

LET ALPHA2=1-(ALPHA/2)
LET SE1=MSE/SUM
LET T1=B1/SQRT(SE1)
LET DF=N-2

INVCDF ALPHA2 TTAB1;
T DF.

CDF T1 P1;
T DF.
LET PVAL1=2*(1-P1)

LET SE0=MSE*((1/N)+(RATAX^2/SUM))
LET T0=B0/SQRT(SE0)

INVCDF ALPHA2 TTAB0;
T DF.

CDF T0 P0;
T DF.
LET PVAL0=2*(1-P0)
PRINT SE1 T1 PVAL1 SE0 T0 PVAL0

LET NILAI=B0+B1*X1
PRINT NILAI 

ENDMACRO

