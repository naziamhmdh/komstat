MACRO

REGANDA X.1-X.P Y ALPHA
MCONSTANT n ybar ssr sse sst dfr dfe dft msr mse mst fhit p pv pvalue btxty yty a ftab alpha n1 i p1 a1 ttab thita
MCOLUMN X.1-X.P Y SATU B VARBJ SEBJ THIT PVALUE1 TTABI
MMATRIX MX MY XT XTX XTXI XTY BT BTXT YT VARKOV

#PREPARATION
LET N=COUNT(Y)

SET SATU 
1(1:1/1)N
END.

COPY SATU X.1-X.P MX
COPY Y MY

#ESTIMATION
TRANSPOSE MX XT
MULT XT MX XTX
INVERT XTX XTXI
MULT XT Y XTY
MULT XTXI XTY B

#TABEL ANOVA
LET YBAR=MEAN(Y)

TRANSPOSE B BT
MULT BT XT BTXT
MULT BTXT MY BTXTY
LET SSR=BTXTY-N*YBAR^2
TRANSPOSE MY YT
MULT YT MY YTY
LET SST=YTY-N*YBAR^2
LET SSE=SST-SSR

LET DFR=P
LET DFE=N-P-1
LET DFT=N-1

LET MSR=SSR/DFR
LET MSE=SSE/DFE
LET FHIT=MSR/MSE

CDF FHIT PV;
F DFR DFE.

LET PVALUE=1-PV

LET A=1-ALPHA

INVCDF A FTAB;
F DFR DFE.

NOTE ANOVA
PRINT DFR DFE DFT SSR SSE SST MSR MSE FHIT FTAB PVALUE ALPHA

IF FHIT>FTAB
  NOTE KEPUTUSAN : TOLAK H0 (MINIMAL TERDAPAT SATU BJ >< 0, J=1,2,...,K)
ELSE
  NOTE KEPUTUSAN : GAGAL TOLAK H0 (B1=B1=..=BK=0)
ENDIF

IF PVALUE<ALPHA
  NOTE KEPUTUSAN : TOLAK H0 (MINIMAL TERDAPAT SATU BJ >< 0, J=1,2,...,K)
ELSE
  NOTE KEPUTUSAN : GAGAL TOLAK H0 (B1=B1=..=BK=0)
ENDIF

#UJI PARSIAL
NOTE UJI PARSIAL

MULT XTXI MSE VARKOV
DIAG VARKOV VARBJ
LET SEBJ=SQRT(VARBJ)

LET THIT=B/SEBJ

LET A1=1-ALPHA/2
INVCDF A1 TTAB;
T DFE.

LET N1=COUNT(B)

LET THITA=0
DO I=1:N1
  LET THITA=ABS(THIT(I))
  CDF THITA P1;
  T DFE.
  LET PVALUE1(I)=2*(1-P1)
  LET TTABI(I)=TTAB
ENDDO
PRINT B SEBJ THIT TTABI PVALUE1

ENDMACRO
