MACRO

TWOVAR X Y ALPHA

MCONSTANT ALPHA NX NY VARX VARY DF1 DF2 FHIT FTAB1 FTAB2 FTAB3 ALPHA1 ALPHA2 LL UL P PVALUE
MCOLUMN X Y

LET NX=COUNT(X)
LET NY=COUNT(Y)

LET VARX=STDEV(X)^2
LET VARY=STDEV(Y)^2

LET DF1=NX-1
LET DF2=NY-1

#FHIT
LET FHIT=VARX/VARY

#FTAB
LET ALPHA1=ALPHA/2
LET ALPHA2=1-ALPHA1
INVCDF ALPHA1 FTAB1;
   F DF1 DF2.
INVCDF ALPHA2 FTAB2;
   F DF1 DF2.
PRINT FHIT FTAB1 FTAB2


IF FHIT<FTAB1 | FHIT>FTAB2
  NOTE Tolak H0, maka varians X dan Y tidak sama
ELSE
  NOTE Gagal tolak H0, maka varians X dan Y sama
ENDIF

#CI
INVCDF ALPHA2 FTAB3;
   F DF2 DF1.
LET LL=FHIT/FTAB2
LET UL=FHIT*FTAB3
PRINT LL UL
IF 1<LL | 1>UL
 NOTE Tolak H0, maka varians X dan Y tidak sama
ELSE
  NOTE Gagal tolak H0, maka varians X dan Y sama
ENDIF

#P-VALUE
CDF FHIT P;
  F DF1 DF2.
LET PVALUE=2*P
PRINT PVALUE ALPHA
IF PVALUE<ALPHA
  NOTE Tolak H0, maka varians X dan Y tidak sama
ELSE
  NOTE Gagal tolak H0, maka varians X dan Y sama
ENDIF

ENDMACRO